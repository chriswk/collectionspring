buildscript {
	repositories {
		maven { url 'http://repo.springsource.org/plugins-release' }
		add(new org.apache.ivy.plugins.resolver.URLResolver()) {
            name = 'GitHub'
            addArtifactPattern 'http://cloud.github.com/downloads/[organisation]/[module]/[module]-[revision].[ext]'
        }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:docbook-reference-plugin:0.1.5'
        classpath 'bmuschko:gradle-tomcat-plugin:0.9.4'
    }
}

configure(allprojects) {
	apply plugin: 'java'
	apply plugin: 'idea'
	apply from: "${rootDir}/dependencies.gradle"
	group = 'com.chriswk.collection'

	sourceCompatability="${javaVersion}"
	targetCompatability="${javaVersion}"
	repositories {
		mavenCentral()
		mavenLocal()
		maven { url "http://repo.springsource.org/libs-release" }
        maven { url "http://repo.springsource.org/milestone" }
	}
}

configure(subprojects) { subproject ->
    apply from: "${rootProject.projectDir}/publish-maven.gradle"
    jar {
        manifest.attributes['Implementation-Title'] = subproject.name
        manifest.attributes['Implementation-Version'] = subproject.version

        from("${rootProject.projectDir}/src/dist") {
            include "license.txt"
            include "notice.txt"
            into "META-INF"
            expand(copyright: new Date().format('yyyy'), version: project.version)
        }
    }

    javadoc {
        options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
        options.author = true
        options.header = project.name
        //options.overview = "${projectDir}/src/main/java/overview.html"
    }

    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

task wrapper(type: Wrapper, description : "Generates the wrapper for ${gradleVersion}") {
	gradleVersion = gradleVersion
}